// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        releaseConfiguration = [
                releaseVersion    : "1.0.1",
                releaseVersionCode: 10000,
        ]
    }

    repositories {
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { project ->
    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle, group: 'check') {
        configFile rootProject.file('checkstyle/checkstyle.xml')
        configProperties.checkstyleJavaHeader = rootProject.file('checkstyle/java.header')
        source 'src/main/java', 'src/test/java', 'src/androidTest/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }
}